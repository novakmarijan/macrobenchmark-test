plugins {
    id 'com.android.test'
    id 'kotlin-android'
}

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdk 29
        targetSdk 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    targetProjectPath = ":app" // Note that your module name may be different
    experimentalProperties["android.experimental.self-instrumenting"] = true
    buildTypes {
        // Declare a build type (release) to match the target app's build type
        release {
            debuggable = true
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

androidComponents {
    beforeVariants(selector().all()) {
        // Enable only the release buildType, since we only want to measure
        // release build performance
        enabled = buildType == 'release'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'junit:junit:4.13.2'
    implementation 'androidx.test.ext:junit:1.1.3'
    implementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation 'androidx.benchmark:benchmark-macro-junit4:1.1.0-alpha03'
}